DEFAULT_DATABASE_CONNECTION_URI := "sqlite:///tmp/neuronek.sqlite"
schema_format := "{{ sql . }}"
dev_db := "sqlite://dev?mode=memory"
migration_dir := "./migrations"
schema_file := "schema.sql"

inspect connection_uri=DEFAULT_DATABASE_CONNECTION_URI:
    atlas schema inspect -u {{ connection_uri }}

pull connection_uri=DEFAULT_DATABASE_CONNECTION_URI:
    atlas schema inspect -u "{{ connection_uri }}" --format '{{ schema_format }}' > {{ schema_file }}

push connection_uri=DEFAULT_DATABASE_CONNECTION_URI:
    atlas schema apply \
      -u "{{ connection_uri }}" \
      --to file://schema.sql \
      --dev-url "{{ dev_db }}"

plan migration_name connection_uri=DEFAULT_DATABASE_CONNECTION_URI:
    @just hash
    atlas migrate diff {{ migration_name }} \
      --dir "file://{{ migration_dir }}" \
      --to "file://{{ schema_file }}" \
      --dev-url "{{ dev_db }}"

create migration_name:
    atlas migrate new {{ migration_name }}

up connection_uri=DEFAULT_DATABASE_CONNECTION_URI:
    @just hash
    atlas migrate apply \
      --url "{{ connection_uri }}"

hash:
    atlas migrate hash --dir="file://{{ migration_dir }}"

fresh:
    @just hash
    @just clean
    @just up
    @just pull

clean connection_uri=DEFAULT_DATABASE_CONNECTION_URI:
    atlas schema clean -u "{{ connection_uri }}" --auto-approve

baseline connection_uri=DEFAULT_DATABASE_CONNECTION_URI:
    atlas migrate diff baseline \
      --dir "file://{{ migration_dir }}" \
      --dev-url "{{ dev_db }}" \
      --to "{{ connection_uri }}" \

generate:
    sea-orm-cli generate entity \
        --compact-format \
        --ignore-tables=seaql_migrations,atlas_schema_revisions \
        --output-dir="./entities" \
        -u="{{ DEFAULT_DATABASE_CONNECTION_URI }}" \
        --with-serde=both \
        --with-copy-enums
